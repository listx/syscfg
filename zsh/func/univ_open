# open up directories and files intelligently

univ_open() {
    ls="ls -Ahs --color=auto"
    if [[ -z $@ ]]; then
        # if we do not provide any arguments, go to the home directory
        cd && ${=ls}
    elif [[ ! -e $@ ]]; then
        # go to the nearest valid parent directory if file does not exist; we
        # use a while loop to find the closest valid directory, just in case
        # the user gave a butchered-up path
        d=$@:h
        while [[ ! -d $d ]]
        do
            d=$d:h
        done
        cd $d && ${=ls}
    elif [[ -d $@ ]]; then
        cd $@ && ${=ls}
    else
        case $@:e:l in
            (doc|odf|odt|rtf)
                soffice -writer $@ &>/dev/null & disown
            ;;
            (htm|html)
                firefox $@ &>/dev/null & disown
            ;;
            (pdf|ps)
                evince $@ &>/dev/null & disown
            ;;
            (bmp|gif|jpg|jpeg|png|svg|tiff)
                eog $@ &>/dev/null & disown
            ;;
            (psd|xcf)
                gimp $@ &>/dev/null & disown
            ;;
            (aac|flac|mp3|ogg|wav|wma)
                smplayer $@ &>/dev/null & disown
            ;;
            (mid|midi)
                timidity $@
            ;;
            (avi|flv|ogv|mkv|mov|mp4|mpg|mpeg|wmv)
                smplayer $@ &>/dev/null & disown
            ;;
            *)
            vim $@
            ;;
        esac
    fi
}
