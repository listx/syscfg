# listdata prompt theme

prompt_listdata_help () {
  cat <<'EOF'
This prompt is color-scheme-able.
EOF
}

# prompt_redhat_setup () {
#  PS1="[%n@%m %1~]\\$ "
#  PS2="> "
#
#  prompt_opts=( cr percent )
#}

# variables used in the prompt, such as the kinds of characters used, and colors

prompt_listdata_setup () {
  # Some can't be local
  local prompt_gfx_tlc prompt_gfx_mlc prompt_gfx_blc

  if [[ $1 == '8bit' ]]; then
    shift
    if [[ ${LC_ALL:-${LC_CTYPE:-$LANG}} = *UTF-8* ]]; then
      prompt_gfx_tlc=$'\xe2\x94\x8c'
      prompt_gfx_mlc=$'\xe2\x94\x9c'
      prompt_gfx_blc=$'\xe2\x94\x94'
      prompt_gfx_hyphen=$'\xe2\x94\x80'
    else
      prompt_gfx_tlc=$'\xda'
      prompt_gfx_mlc=$'\xc3'
      prompt_gfx_blc=$'\xc0'
      prompt_gfx_hyphen=$'\xc4'
    fi
  else
    prompt_gfx_tlc=''
    prompt_gfx_mlc='|'
    prompt_gfx_blc=''
    prompt_gfx_hyphen=' '
  fi

  # Colour scheme
  prompt_listdata_color1=${1:-'cyan'}    # hyphens
  prompt_listdata_color2=${2:-'blue'}   # current directory
  prompt_listdata_color3=${3:-'default'}    # user@host
  prompt_listdata_color4=${4:-'white'}   # user input
  # misc colors
  prompt_listdata_color5=${5:-'red'}
  prompt_listdata_color6=${6:-'green'}
  prompt_listdata_color7=${7:-'yellow'}
  prompt_listdata_color8=${8:-'magenta'}

  local prompt_gfx_bbox
  prompt_gfx_tbox="%B%F{$prompt_listdata_color1}${prompt_gfx_tlc}%b%F{$prompt_listdata_color1}${prompt_gfx_hyphen}"
  prompt_gfx_bbox="%B%F{$prompt_listdata_color1}${prompt_gfx_blc}${prompt_gfx_hyphen}%b%F{$prompt_listdata_color1}"

  # This is a cute hack.  Well I like it, anyway.
  prompt_gfx_bbox_to_mbox=$'%{\e[A\r'"%}%B%F{$prompt_listdata_color1}${prompt_gfx_mlc}%b%F{$prompt_listdata_color1}${prompt_gfx_hyphen}%{"$'\e[B%}'

  prompt_l_paren="%B%F{white}["
  prompt_r_paren="%B%F{white}]"

  hostcolor=""
  case $(hostname) in
    exelion)
        hostcolor=$prompt_listdata_color4
    ;;
    luxion)
        hostcolor=$prompt_listdata_color6
    ;;
    aether)
        hostcolor=$prompt_listdata_color1
    ;;
    ocean)
        hostcolor=$prompt_listdata_color7
    ;;
    forest)
        hostcolor=$prompt_listdata_color8
    ;;
    *)
        hostcolor=$prompt_listdata_color5
    ;;
  esac

  prompt_user_host="%b%F{$prompt_listdata_color3}%n%B%F{$prompt_listdata_color4}@%b%F{$hostcolor}%m"

  prompt_line_1a="$prompt_gfx_tbox$prompt_l_paren%b%F{$prompt_listdata_color3}%* %B%F{$prompt_listdata_color4}| %F{$prompt_listdata_color1}%~ $prompt_r_paren%b%F{$prompt_listdata_color1}"
  prompt_line_1b="$prompt_l_paren$prompt_user_host$prompt_r_paren%b%F{$prompt_listdata_color1}${prompt_gfx_hyphen}"

  prompt_line_2="$prompt_gfx_bbox${prompt_gfx_hyphen}%B%F{white}"

  prompt_char="%(!.# .>"

  prompt_opts=(cr subst percent)

  add-zsh-hook precmd prompt_listdata_precmd
}

# the actual prompt

prompt_listdata_precmd() {
  setopt noxtrace localoptions extendedglob
  local prompt_line_1

  prompt_listdata_choose_prompt

  charcolor=""
  case $(whoami) in
    listdata)
        charcolor=$prompt_listdata_color4
    ;;
    root)
        charcolor=$prompt_listdata_color5
    ;;
    *)
        charcolor=$prompt_listdata_color2
    ;;
  esac

  PS1="$prompt_line_1$prompt_newline$prompt_line_2%B%F{$charcolor}$prompt_char %b%f%k"
  #PS2="$prompt_line_2$prompt_gfx_bbox_to_mbox%B%F{white}%_> %b%f%k"
  #PS3="$prompt_line_2$prompt_gfx_bbox_to_mbox%B%F{white}?# %b%f%k"
  zle_highlight[(r)default:*]="default:fg=$charcolor,bold"
}

# resize the prompt according to the working directory's length

prompt_listdata_choose_prompt () {
  local prompt_line_1a_width=${#${(S%%)prompt_line_1a//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local prompt_line_1b_width=${#${(S%%)prompt_line_1b//(\%([KF1]|)\{*\}|\%[Bbkf])}}

  local prompt_padding_size=$(( COLUMNS
                                  - prompt_line_1a_width
                                  - prompt_line_1b_width ))

  # Try to fit in long path and user@host.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}::${prompt_gfx_hyphen}:)_empty_zz}"
    prompt_line_1="$prompt_line_1a$prompt_padding$prompt_line_1b"
    return
  fi

  prompt_padding_size=$(( COLUMNS - prompt_line_1a_width ))

  # Didn't fit; try to fit in just long path.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}::${prompt_gfx_hyphen}:)_empty_zz}"
    prompt_line_1="$prompt_line_1a$prompt_padding"
    return
  fi

  # Still didn't fit; truncate
  local prompt_pwd_size=$(( COLUMNS - 5 ))
  prompt_line_1="$prompt_gfx_tbox$prompt_l_paren%B%F{$prompt_listdata_color2}%$prompt_pwd_size<...<%~%<<$prompt_r_paren%b%F{$prompt_listdata_color1}$prompt_gfx_hyphen"
}

prompt_listdata_setup "$@"
